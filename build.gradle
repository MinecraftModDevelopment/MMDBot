plugins {
    id 'application'
    id 'com.github.johnrengelman.shadow' version '4.0.4'
    id 'eclipse'
    id 'idea'
    id 'com.github.spotbugs' version '1.6.9' // 4.0 or later
    id 'checkstyle' // 2.7 or later
    id 'pmd'
    id "org.jetbrains.kotlin.jvm" version "1.4.21"
}

mainClassName = 'com.mcmoddev.mmdbot.MMDBot'
group 'com.mcmoddev'
version project.bot_version

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    jcenter()
    mavenCentral()
    maven {
        url 'https://jitpack.io'
    }
    maven {
        url "https://maven.shedaniel.me/"
    }
    maven {
        url "https://maven.fabricmc.net"
    }
}

dependencies {
    implementation "com.jagrosh:jda-utilities:${project.jdautils_version}"
    implementation "net.dv8tion:JDA:${project.jda_version}"
    implementation "com.google.guava:guava:${project.guava_version}"
    implementation "com.google.code.gson:gson:${project.gson_version}"
    implementation "ch.qos.logback:logback-classic:${project.logback_classic_version}"
    implementation "com.electronwill.night-config:toml:${project.nightconfig_version}"
    implementation "me.shedaniel:linkie-core:1.0.60"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.4.21"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.1"

    testImplementation "org.junit.jupiter:junit-jupiter:5.6.0"
}

jar {
    manifest {
        mainAttributes(
                "Maven-Artifact": "${project.group}:${archivesBaseName}:${project.bot_version}",
                "Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "Specification-Title": archivesBaseName,
                "Specification-Vendor": "MinecraftModDevs",
                "Specification-Version": "1",
                "Implementation-Title": archivesBaseName,
                "Implementation-Version": "${project.bot_version}",
                "Implementation-Vendor": "MinecraftModDevs",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "Built-On-Java": "${System.getProperty('java.vm.version')} (${System.getProperty('java.vm.vendor')})",
                "Built-On": "${project.jda_version}-${project.jdautils_version}"
        )
    }
}

compileJava.options.encoding = 'UTF-8'

eclipse.project {
  buildCommand 'org.eclipse.buildship.core.gradleprojectbuilder'
  natures 'org.eclipse.buildship.core.gradleprojectnature'
}

idea.module {
  downloadJavadoc = true
  //inheritOutputDirs = true
}

javadoc {
  failOnError = false
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

spotbugs {
  ignoreFailures = true
  toolVersion = '3.1.6'
  effort = 'max'
  reportLevel = 'low'
  //includeFilter = file("$rootProject.projectDir/config/spotbugs-filter.xml")
}

tasks.withType(com.github.spotbugs.SpotBugsTask) {
  reports {
    xml.enabled = false
    html.enabled = true
  }
}

checkstyle {
  ignoreFailures = true
  toolVersion = '8.20' // 5.7 is default
  showViolations = false
}

pmd {
  consoleOutput = false
  ignoreFailures = true
  sourceSets = [sourceSets.main]
  reportsDir = file("$project.buildDir/reports/pmd")
  rulePriority = 5
  ruleSets = [
    'java-android',
    'java-basic',
    'java-braces',
    'java-clone',
    'java-codesize',
    'java-comments',
    'java-controversial',
    'java-coupling',
    'java-design',
    'java-empty',
    'java-finalizers',
    'java-imports',
    'java-j2ee',
    'java-javabeans',
    'java-junit',
    'java-logging-jakarta-commons',
    'java-logging-java',
//    'java-metrics', // Upsets 5.8.1
    'java-migrating',
    'java-migrating_to_junit4',
    'java-naming',
    'java-optimizations',
//    'java-quickstart', // 6.x
    'java-strictexception',
    'java-strings',
    'java-sunsecure',
    'java-typeresolution',
    'java-unnecessary',
    'java-unusedcode'
  ]
  toolVersion = '5.8.1' // 6.19.0
//  incrementalAnalysis = true // Requires PMD 6.0.0 or newer
}
